
using { /Fortnite.com/Devices }
using { /Verse.org/Simulation }
using { /UnrealEngine.com/Temporary/Diagnostics }
using { /Verse.org/Assets }
using { /UnrealEngine.com/Temporary/SpatialMath }
using { /Fortnite.com/Characters }
using { /Verse.org/Simulation/Tags }

CollisionEventParams := class:  #For Passing / Disposing Spawned Prop Used For Casting
    _Prop1 : creative_prop

Collidables := class(tag): #Props with this tag must be the same mesh with the same default dimensions as the prop you're spawning / casting forward

Orb_Casting := class(creative_device):  #Jenky "Ray-ish" Casting

    CollisionEvent : event(CollisionEventParams) = event(CollisionEventParams){}    #Signaled when collision is detected and passes prop for disposal

    @editable CastObject : creative_prop_asset = DefaultCreativePropAsset   #Must Match the prop that has the collidable tag in terms of default Length, Width, and Height

    TouchingMat : material = Mat0.MI_Touching   #Only for visualization
    NotTouchingMat : material = Mat0.MI_Not_Touching    #Only for visualization

    var _Collidables :[]creative_prop = array{} #For Storing Valid Props With Tags

    @editable OrbCaster : input_trigger_device = input_trigger_device{}

    OnBegin<override>()<suspends>:void=
        Collidable_Objects := GetCreativeObjectsWithTag(Collidables{})
        for(Item : Collidable_Objects):
            if(Prop := creative_prop[Item]):
                if(Prop.IsValid[]):
                    set _Collidables += array{Prop}

        OrbCaster.PressedEvent.Subscribe(StageFortCharacter)    #Gets Fort Char From Agent and starts Casting Orb for that player
        spawn{AwaitCollisionEvent()}    #Loop that awaits and handles collision events

    StageFortCharacter(Agent:agent):void=
        if(Character := Agent.GetFortCharacter[]):
            spawn:
                CastOrb(Character)

    CollisionLoop(Orb:creative_prop)<suspends>:void=
        loop:   #This could be optimized. Could Maybe Grab the closest X amount of props before checking collision instead of all but this is fine for testing
            Sleep(0.1)
            for(Prop:_Collidables):
                if(Orb.IsValid[]):
                    if(Prop.IsValid[]):
                        HasCollided := IsColliding(Orb, Prop)
                        if(HasCollided = true):
                            Sleep(5.0)
                            break

    GetRadius(Prop:creative_prop):float= 
        if(Prop.IsValid[]):
            DefaultRadius := 100.0  #if you're not using blenders default UV Sphere you must know your mesh's default radius in cm and update this value here
            _Scale := Prop.GetTransform().Scale
            AvgScale := (_Scale.X + _Scale.Y + _Scale.Z) / 3.0  #Scale Must Be Uniform on all axes
            Radius := DefaultRadius * AvgScale  #Scale Will Alter the overall radius
            return Radius
        return 100.0

    AwaitCollisionEvent()<suspends>:void=
        loop:
            Task := CollisionEvent.Await()
            Print("Signal Recieved")
            Prop := Task._Prop1
            if(Prop.IsValid[]):
                Prop.Dispose()

    IsColliding(Sphere_A:creative_prop, Sphere_B:creative_prop):logic=
        Prop1Radius := GetRadius(Sphere_A)
        Prop2Radius := GetRadius(Sphere_B)
        if(Sphere_A.IsValid[] and Sphere_B.IsValid[]):
            Dist := Distance(Sphere_A.GetTransform().Translation, Sphere_B.GetTransform().Translation)
            if( Dist < (Prop1Radius + Prop2Radius) ):
                #Print("Objects Colliding")
                Sphere_A.SetMaterial(TouchingMat)  #Only for visualization
                Sphere_B.SetMaterial(TouchingMat)  #Only for visualization
                CollisionEvent.Signal(CollisionEventParams{_Prop1 := Sphere_A}) # Passes the spawned orb that collided for disposal
                return true
            #Print("Objects Do Not Collide")
            Sphere_A.SetMaterial(NotTouchingMat)   #Only for visualization
            Sphere_B.SetMaterial(NotTouchingMat)   #Only for visualization
        else:
            Print("Not Valid")
        return false

    CastOrb(Character:fort_character)<suspends>:void=
        var DistanceTraveled : float = 0.0
        InitialLocation := Character.GetTransform().Translation #init loc for spawning orb to cast
        InitialRotation := Character.GetViewRotation()
        YPR := InitialRotation.GetYawPitchRollDegrees()
        if(Pitch := YPR[1]):    #Get Pitch For Future Offset / Calculating a new Z height when casting
            if:
                Prop := SpawnProp(CastObject, InitialLocation, IdentityRotation())(0)?
                Prop.IsValid[]
                Prop.TeleportTo[InitialLocation, InitialRotation]   #Prob don't need this
            then:
                if(Prop.IsValid[]):
                    spawn:
                        CollisionLoop(Prop) #Check Collision for this this spawned orb
                loop:
                    if(DistanceTraveled >= 25000.0):    #max distance orb can travel before being disposed. 
                        break
                    Sleep(0.01)
                    if(Prop.IsValid[]):
                        LocalForwardDirection := Prop.GetTransform().Rotation.GetLocalForward() #0 - 1 range
                        NormalizedLocalForward := Normalize(LocalForwardDirection) # -1 to 1 range
                        NormalizedPitch := NormalizePitch(Pitch) # for getting a good height offset
                        TravelDist := 100.0 #cm to increment the orb by
                        X_Offset := NormalizedLocalForward.X * TravelDist   # gets the normalized new directions to be a sizable number for offsetting the last translation
                        Y_Offset := NormalizedLocalForward.Y * TravelDist
                        Z_Offset := NormalizedPitch * TravelDist
                        TargetLocation := vector3{X := Prop.GetTransform().Translation.X + X_Offset, Y := Prop.GetTransform().Translation.Y + Y_Offset, Z := Prop.GetTransform().Translation.Z + Z_Offset}  #Add Offsets For Casting Forward
                        if(Prop.IsValid[]):
                            Prop.MoveTo(TargetLocation, Prop.GetTransform().Rotation, 0.02)
                            set DistanceTraveled += 100.0
                        else:
                            break
                    else:
                        break
                if(Prop.IsValid[]):
                    Prop.Dispose()
